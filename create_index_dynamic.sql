SPOOL C:\temp\UNINDEX_FILIPE.sql
SET SERVEROUT ON TRIMSPOOL ON LINES 1000 FEED OFF
DECLARE
	V_INDEX NUMBER:=0;
	V_QTDSUCC NUMBER:=0;
	V_QTDERRO NUMBER:=0;
	V_INDEXNAME VARCHAR2(30);
	V_COMANDO VARCHAR2(4000);
	V_COLUNAS VARCHAR2(3970);
	TYPE T_VARCHAR2 IS TABLE OF VARCHAR2(30);
	V_TBLINDEXNAME T_VARCHAR2:= T_VARCHAR2(null);
	FUNCTION ADMDADOS_GET_INDEX_NOT_EXISTS (
		P_TABELA IN VARCHAR2
	)	
	RETURN VARCHAR2	AS
		V_INDEXNAMETMP VARCHAR2(30);
		V_QUANTIDADETBL   NUMBER;
		V_QUANTIDADEVET   NUMBER;
	BEGIN
		FOR IDX IN 1..99 LOOP
			V_QUANTIDADETBL:=0;
			V_QUANTIDADEVET:=0;
			V_INDEXNAMETMP:=SUBSTR(P_TABELA, 0, 23)||'_PIDX'|| LPAD(IDX, 2, '0');
			SELECT COUNT(1) INTO V_QUANTIDADETBL FROM USER_INDEXES WHERE INDEX_NAME=V_INDEXNAMETMP;
			IF (V_QUANTIDADETBL=0) THEN
				FOR IDX2 IN V_TBLINDEXNAME.FIRST..V_TBLINDEXNAME.LAST LOOP
					IF (V_TBLINDEXNAME(IDX2) = V_INDEXNAMETMP) THEN
						V_QUANTIDADEVET:=1;
						EXIT;
					END IF;
				END LOOP;
				IF (V_QUANTIDADEVET=0) THEN
					V_INDEX:=V_INDEX+1;
					V_TBLINDEXNAME.EXTEND();
					V_TBLINDEXNAME(V_INDEX):=V_INDEXNAMETMP;
					EXIT;
				END IF;
			END IF;
		END LOOP;
		RETURN V_INDEXNAMETMP;
	END ADMDADOS_GET_INDEX_NOT_EXISTS;
BEGIN
	V_COMANDO:='';
	V_COLUNAS:='';
	FOR IDX IN (	SELECT 
					FK.CONSTRAINT_NAME		NOME, 
					FK.TABLE_NAME			TABELA, 
					FK.COLUMN_NAME			COLUNA, 
					FK.POSITION				POSICAO, 
					FKSEMINDICE.QUANTIDADE	QUANTIDADE
					FROM USER_CONS_COLUMNS FK 
					INNER JOIN (SELECT FK.NOME NOME, FK.QUANTIDADE QUANTIDADE 
								FROM (SELECT 	UCC.CONSTRAINT_NAME NOME, 
												UCC.TABLE_NAME TABELA, 
												UCC.COLUMN_NAME COLUNA, 
												POSITION POSICAO, 
												COUNT(CONSTRAINT_NAME) OVER (PARTITION BY CONSTRAINT_NAME) QUANTIDADE 
												FROM USER_CONS_COLUMNS UCC 
												WHERE EXISTS (SELECT 1 
																FROM USER_CONSTRAINTS UC 
																WHERE UC.CONSTRAINT_TYPE='R' 
																--AND DELETE_RULE<>'NO ACTION' 
																AND UC.R_CONSTRAINT_NAME IN (SELECT CONSTRAINT_NAME FROM USER_CONSTRAINTS WHERE CONSTRAINT_TYPE IN ('U', 'P') AND TABLE_NAME IN ('SPEDPARCCONTAB_SPCC', 'PLANOCTA_PLC'))
																AND UC.CONSTRAINT_NAME=UCC.CONSTRAINT_NAME)) FK
					WHERE NOT EXISTS (SELECT 1 
					FROM (SELECT 	INDEX_NAME NOME, 
									TABLE_NAME TABELA, 
									COLUMN_NAME COLUNA, 
									COLUMN_POSITION POSICAO, 
									COUNT(INDEX_NAME) OVER (PARTITION BY INDEX_NAME) QUANTIDADE 
						FROM USER_IND_COLUMNS ) INDICE
						WHERE INDICE.TABELA=FK.TABELA 
						AND INDICE.COLUNA=FK.COLUNA 
						AND INDICE.POSICAO=FK.POSICAO 
						AND INDICE.QUANTIDADE=FK.QUANTIDADE
						) GROUP BY FK.NOME, FK.QUANTIDADE) FKSEMINDICE
				ON (FKSEMINDICE.NOME=FK.CONSTRAINT_NAME)
				
				ORDER BY FK.TABLE_NAME, FK.CONSTRAINT_NAME, FK.POSITION) 
	LOOP		
		CASE 
		WHEN (IDX.POSICAO='1') THEN
			V_COLUNAS:= '"'||IDX.COLUNA||'"';			
		ELSE
			V_COLUNAS:= V_COLUNAS||', "'||IDX.COLUNA||'"';
		END CASE;
		IF (IDX.POSICAO=IDX.QUANTIDADE) THEN
			V_INDEXNAME:=ADMDADOS_GET_INDEX_NOT_EXISTS(P_TABELA=>IDX.TABELA);	
			V_COMANDO:='CREATE INDEX '|| RPAD('"'|| V_INDEXNAME || '"', 32, ' ')|| ' ON ' || RPAD('"'|| IDX.TABELA || '"', 32, ' ') || ' (' || V_COLUNAS || ') COMPUTE STATISTICS';
			BEGIN
				DBMS_OUTPUT.PUT_LINE(V_COMANDO||';');
				--EXECUTE IMMEDIATE V_COMANDO;
				--DBMS_OUTPUT.PUT_LINE(CHR(10)||'Indice Criado.'||CHR(10));
				V_QTDSUCC:=V_QTDSUCC+1;
				EXCEPTION WHEN OTHERS THEN 
				BEGIN
					V_QTDERRO:=V_QTDERRO+1;
					DBMS_OUTPUT.PUT_LINE(CHR(10)||SQLERRM||CHR(10));
				END;
			END;
			V_COMANDO:='';
			V_COLUNAS:='';
		END IF;
	END LOOP;
END;
/
SPOOL OFF
ED C:\temp\UNINDEX_FILIPE.sql
